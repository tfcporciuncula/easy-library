buildscript {
  apply from: rootProject.file('versions&dependencies.gradle')

  repositories {
    mavenCentral()
    google()
    jcenter()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:$versions.agp"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
  }
}

plugins {
  id 'org.jmailen.kotlinter' version '2.3.0' apply false
}

subprojects {
  repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    jcenter()
  }

  afterEvaluate { project ->
    if (project.hasProperty('android')) {
      android {
        compileSdkVersion buildConfig.compileSdk
        buildToolsVersion buildConfig.buildTools

        defaultConfig {
          minSdkVersion buildConfig.minSdk
          targetSdkVersion buildConfig.targetSdk
          versionCode 1
          versionName '1.0'
        }

        compileOptions {
          sourceCompatibility JavaVersion.VERSION_1_8
          targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
          jvmTarget = JavaVersion.VERSION_1_8
        }
      }
    }
  }

  apply plugin: 'org.jmailen.kotlinter'

  kotlinter {
    indentSize = 2
    experimentalRules = true
    disabledRules = ["import-ordering"]
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}
