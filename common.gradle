afterEvaluate { project ->
  if (project.hasProperty('android')) {
    android {
      compileSdkVersion buildConfig.compileSdk
      buildToolsVersion buildConfig.buildTools

      defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode appVersion.major * 1000000 + appVersion.minor * 10000 + appVersion.patch * 100 + appVersion.build
        versionName "$appVersion.major.$appVersion.minor.$appVersion.patch" + (appVersion.snapshot ? '-SNAPSHOT' : '')
      }

      compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
      }

      kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
      }

      buildTypes {
        debug {
          versionNameSuffix appVersion.snapshot ? '-dev' : ".$appVersion.build-dev"
        }
      }

      sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
      }
    }

    extensions.findByName('kapt')?.arguments {
      arg('dagger.fastInit', 'enabled')
    }
  }
}
